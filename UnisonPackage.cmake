include(../cmake/UnisonVersion.cmake)

set(PROJECT_VENDOR "Unison Inc.")
set(PROJECT_COPYRIGHT_YEAR "2012-2013")
set(PROJECT_DOMAIN_FIRST "unison")
set(PROJECT_DOMAIN_SECOND "com")
set(PROJECT_DOMAIN "${PROJECT_DOMAIN_FIRST}.${PROJECT_DOMAIN_SECOND}")

set(PROJECT_APPLICATION_NAME "Unison3")
set(PROJECT_EXECUTABLE_NAME ${TARGET})

set(CPACK_PACKAGE_FILE_NAME "${PROJECT_NAME}-${UNISON_PACKAGE_VERSION}")
set(CPACK_PACKAGE_VERSION_MAJOR "${UNISON_VERSION_MAJOR}")
set(CPACK_PACKAGE_VERSION_MINOR "${UNISON_VERSION_MINOR}")
set(CPACK_PACKAGE_VERSION_PATCH "${UNISON_VERSION_PATCH}")
set(CPACK_PACKAGE_VERSION "${UNISON_VERSION}")
set(CPACK_PACKAGE_VENDOR "${PROJECT_VENDOR}")
add_definitions(-DUNISON_VERSION=\"${UNISON_VERSION}\")

if(UNIX AND NOT APPLE)
    string(TOLOWER ${PROJECT_APPLICATION_NAME} PROJECT_APPLICATION_NAME_LOWERCASE)
    set(BIN_INSTALL_DIR "bin")
    set(DOC_INSTALL_DIR "share/doc/${PROJECT_APPLICATION_NAME_LOWERCASE}/")
else()
    set(BIN_INSTALL_DIR ".")
    set(DOC_INSTALL_DIR ".")
endif()

set(ICONS_DIR "${CMAKE_SOURCE_DIR}/src/resources/icons")

message(STATUS "${PROJECT_EXECUTABLE_NAME} will be installed to ${CMAKE_INSTALL_PREFIX}")
install(TARGETS ${PROJECT_EXECUTABLE_NAME} DESTINATION ${BIN_INSTALL_DIR})

set(LICENSE_FILE "LICENSE.txt")
set(README_FILE "README.md")

set(CPACK_GENERATOR "TBZ2")
set(CPACK_RESOURCE_FILE_README "${CMAKE_SOURCE_DIR}/${README_FILE}")
if(WIN32)
    set(CPACK_GENERATOR "NSIS")
    set(CPACK_PACKAGE_EXECUTABLES "${PROJECT_APPLICATION_NAME}" "${PROJECT_APPLICATION_NAME}")
    set(CPACK_PACKAGE_INSTALL_DIRECTORY "${PROJECT_APPLICATION_NAME}")
    set(CPACK_NSIS_MODIFY_PATH "OFF")
    # set this to 1 to provide license file
    set(CPACK_RESOURCE_FILE_LICENSE_PROVIDED "1")
    if(CPACK_RESOURCE_FILE_LICENSE_PROVIDED STREQUAL "1")
        set(CPACK_RESOURCE_FILE_LICENSE "${CMAKE_SOURCE_DIR}/${LICENSE_FILE}")
    endif()
    set(CPACK_NSIS_EXECUTABLES_DIRECTORY "${BIN_INSTALL_DIR}")
    set(CPACK_NSIS_MUI_ICON "${CMAKE_SOURCE_DIR}/src/resources/application.ico")
    set(CPACK_NSIS_MUI_UNIICON "${CMAKE_SOURCE_DIR}/src/resources/application.ico")
    #set(CPACK_PACKAGE_ICON "${CMAKE_SOURCE_DIR}/src/resources/nsis.bmp")
    set(CPACK_NSIS_URL_INFO_ABOUT "http://${PROJECT_DOMAIN}")
    set(CPACK_NSIS_INSTALLED_ICON_NAME "${PROJECT_EXECUTABLE_NAME}${CMAKE_EXECUTABLE_SUFFIX}")
    set(CPACK_NSIS_MENU_LINKS
        "${CPACK_NSIS_URL_INFO_ABOUT}" "Visit us on the web"
    )
    set(CPACK_NSIS_MUI_FINISHPAGE_RUN "${CPACK_NSIS_INSTALLED_ICON_NAME}")
elseif(APPLE)
    set(MACOSX_BUNDLE_INFO_STRING "${PROJECT_EXECUTABLE_NAME} ${UNISON_VERSION}")
    set(MACOSX_BUNDLE_BUNDLE_VERSION "${PROJECT_EXECUTABLE_NAME} ${UNISON_VERSION}")
    set(MACOSX_BUNDLE_LONG_VERSION_STRING "${PROJECT_EXECUTABLE_NAME} ${UNISON_VERSION}")
    set(MACOSX_BUNDLE_SHORT_VERSION_STRING "${UNISON_VERSION}")
    set(MACOSX_BUNDLE_COPYRIGHT "${PROJECT_COPYRIGHT_YEAR} ${PROJECT_VENDOR}")
    set(MACOSX_BUNDLE_ICON_FILE "unison.icns")
    set(MACOSX_BUNDLE_GUI_IDENTIFIER "${PROJECT_DOMAIN_SECOND}.${PROJECT_DOMAIN_FIRST}.${PROJECT_NAME}")
    set(MACOSX_BUNDLE_BUNDLE_NAME "${PROJECT_APPLICATION_NAME}")

    set(MACOSX_BUNDLE_RESOURCES "${EXECUTABLE_OUTPUT_PATH}/${PROJECT_EXECUTABLE_NAME}.app/Contents/Resources")
    set(MACOSX_RESOURCE_FILES "${ICONS_DIR}/${MACOSX_BUNDLE_ICON_FILE}")
    set_source_files_properties(${MACOSX_RESOURCE_FILES} PROPERTIES MACOSX_PACKAGE_LOCATION Resources)

    execute_process(COMMAND ${CMAKE_COMMAND} -E make_directory ${MACOSX_BUNDLE_RESOURCES})
    execute_process(COMMAND ${CMAKE_COMMAND} -E copy_if_different ${ICONS_DIR}/${MACOSX_BUNDLE_ICON_FILE} ${MACOSX_BUNDLE_RESOURCES}/${MACOSX_BUNDLE_ICON_FILE})
    # qt_menu.nib is mandatory for mac
    if(${QT_USE_FRAMEWORKS})
        set(QT_MENU_NIB ${QT_LIBRARY_DIR}/QtGui.framework/Resources/qt_menu.nib)
    else()
        set(QT_MENU_NIB ${QT_LIBRARY_DIR}/Resources/qt_menu.nib)
    endif()
    execute_process(COMMAND ${CMAKE_COMMAND} -E copy_directory ${QT_MENU_NIB} ${MACOSX_BUNDLE_RESOURCES}/qt_menu.nib)

    set(CMAKE_INSTALL_PREFIX "/Applications")
    set(CPACK_GENERATOR "DragNDrop")
    set(CPACK_DMG_FORMAT "UDBZ")
    set(CPACK_DMG_VOLUME_NAME "${PROJECT_APPLICATION_NAME}")
    set(CPACK_SYSTEM_NAME "OSX")
    set(CPACK_PACKAGE_ICON "${ICONS_DIR}/unison.icns")
    #set(CPACK_DMG_DS_STORE "${ICONS_DIR}/DMGDSStore")
    #set(CPACK_DMG_BACKGROUND_IMAGE "${ICONS_DIR}/DMGBackground.png")
elseif(UNIX)
    set(CPACK_SYSTEM_NAME "${CMAKE_SYSTEM_NAME}-${CMAKE_SYSTEM_PROCESSOR}")
endif()

include(CPack)

set(CMAKE_INSTALL_SYSTEM_RUNTIME_DESTINATION "${BIN_INSTALL_DIR}")
include(InstallRequiredSystemLibraries)

if(APPLE)
    set(EXECUTABLE "${PROJECT_NAME}.app")
elseif(WIN32)
    set(EXECUTABLE "${PROJECT_NAME}${CMAKE_EXECUTABLE_SUFFIX}")
else()
    set(EXECUTABLE "${BIN_INSTALL_DIR}/${PROJECT_NAME}${CMAKE_EXECUTABLE_SUFFIX}")
endif()
