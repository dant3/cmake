# --- functions

function(extract_branch_name GIT_SYMBOLIC_REF BRANCH_NAME)
    string(REPLACE "/" ";" BRANCH_PATH ${GIT_SYMBOLIC_REF})
    list(LENGTH BRANCH_PATH PATH_LENGTH)
    math(EXPR INDEX "${PATH_LENGTH} - 1" )
    list(GET BRANCH_PATH ${INDEX} NEW_BRANCH_NAME)
    set(${BRANCH_NAME} ${NEW_BRANCH_NAME} PARENT_SCOPE)
endfunction()


function(extract_version CURRENT_TAG MAJOR_VERSION MINOR_VERSION PATCH_VERSION COMMIT_HASH)
    string(REPLACE "-" ";" CURRENT_TAG ${CURRENT_TAG})
    list(LENGTH CURRENT_TAG CURRENT_TAG_LENGTH)
    if(${CURRENT_TAG_LENGTH} GREATER 1)
        list(GET CURRENT_TAG 1 NEW_PATCH_VERSION)
    else()
        set(NEW_PATCH_VERSION "0")
    endif()
    set(${PATCH_VERSION} ${NEW_PATCH_VERSION} PARENT_SCOPE)

    if(${CURRENT_TAG_LENGTH} GREATER 2)
        list(GET CURRENT_TAG 2 NEW_COMMIT_HASH)
    else()
        list(GET CURRENT_TAG 0 NEW_COMMIT_HASH)
    endif()
    set(${COMMIT_HASH} ${NEW_COMMIT_HASH} PARENT_SCOPE)

    list(GET CURRENT_TAG 0 CURRENT_TAG)
    string(REPLACE "." ";" CURRENT_TAG_PARTS ${CURRENT_TAG})
    list(LENGTH CURRENT_TAG_PARTS CURRENT_TAG_LENGTH)
    if(${CURRENT_TAG_LENGTH} LESS 2)
        message(FATAL_ERROR "Could not parse version tag [${CURRENT_TAG}]. Tag should contain at least one dot")
    endif()

    list(GET CURRENT_TAG_PARTS 0 NEW_MAJOR_VERSION)
    list(GET CURRENT_TAG_PARTS 1 NEW_MINOR_VERSION)
    set(${MAJOR_VERSION} ${NEW_MAJOR_VERSION} PARENT_SCOPE)
    set(${MINOR_VERSION} ${NEW_MINOR_VERSION} PARENT_SCOPE)
endfunction()

# --- end functions


execute_process(COMMAND git describe --tags --always
                WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
                OUTPUT_VARIABLE CURRENT_TAG
                )
string(STRIP ${CURRENT_TAG} CURRENT_TAG)
extract_version(${CURRENT_TAG} UNISON_VERSION_MAJOR UNISON_VERSION_MINOR UNISON_VERSION_PATCH UNISON_HASH)


execute_process(COMMAND git symbolic-ref HEAD
                WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
                OUTPUT_VARIABLE CURRENT_BRANCH
                )
string(STRIP ${CURRENT_BRANCH} CURRENT_BRANCH)
extract_branch_name(${CURRENT_BRANCH} CURRENT_BRANCH)

# variables exposing
set(UNISON_VERSION "${UNISON_VERSION_MAJOR}.${UNISON_VERSION_MINOR}.${UNISON_VERSION_PATCH}")
string(SUBSTRING ${CURRENT_BRANCH} 0 3 BRANCH_PREFIX)
if(${BRANCH_PREFIX} STREQUAL "rc-")
    set(UNISON_PACKAGE_VERSION "${UNISON_VERSION}")
else()
    set(UNISON_PACKAGE_VERSION "${UNISON_VERSION}-${CURRENT_BRANCH}")
endif()
